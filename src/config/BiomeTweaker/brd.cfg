# o g block rock desert desert
Tweaker.setWorld(1001)

# block rock desert base biome
Tweaker.setStage("BIOME_REGISTRY")
Tweaker.createBiome("block_rock_desert", "minecraft:desert")
Tweaker.createBiome("block_rock_desert_trees", "minecraft:desert")

brd = forBiomes("biometweaker:block_rock_desert")
brd.set("name", "Endless Desert")
brd.set("genVillages", false)
brd.set("height", 0)
brd.set("heightVariation", 0)
brd.removeDicTypes("DRY")
brd.removeDicTypes("HOT")
brd.removeFeature("LAKE")           # unknown if effect?
brd.removeFeature("ANIMALS")
brd.removeDecoration("LAKE_WATER")  # unknown if effect?
brd.removeDecoration("CACTUS")
brd.removeDecoration("TREE")
brd.addToGeneration("HOT", 20000)
brd.addToGeneration("WARM", 20000)
brd.addToGeneration("COOL", 20000)
brd.addToGeneration("ICY", 20000)
brd.registerGenBlockRep("minecraft:water", "minecraft:air")  # definite effect

# variant with trees and a touch of life
brd_tree = forBiomes("biometweaker:block_rock_desert_trees")
brd_tree.set("name", "Boring Desert")
brd_tree.set("genVillages", false)
brd_tree.set("height", 0)
brd_tree.set("heightVariation", 0)
brd_tree.removeFeature("LAKE")           # unknown if effect?
brd_tree.removeDecoration("LAKE_WATER")  # unknown if effect?
brd_tree.removeDecoration("CACTUS")
brd_tree.addToGeneration("HOT", 10000)
brd_tree.addToGeneration("WARM", 10000)
brd_tree.addToGeneration("COOL", 10000)
brd_tree.addToGeneration("ICY", 10000)
brd_tree.registerGenBlockRep("minecraft:water", "minecraft:air")  # definite effect


# update spooky biomes
spooky = forBiomesOfTypes("SPOOKY")
spooky.set("height", 0)
spooky.set("heightVariation", 0)
spooky.set("genVillages", false)
spooky.set("name", "Spooky Desert")
spooky.set("topBlock", "minecraft:sand")
spooky.set("fillerBlock", "minecraft:sandstone")
spooky.removeFeature("LAKE")           # unknown if effect?
spooky.removeDecoration("LAKE_WATER")  # unknown if effect?
spooky.removeDecoration("CACTUS")
spooky.removeDicTypes("FOREST")
spooky.removeDicTypes("HILLS")
spooky.registerGenBlockRep("minecraft:water", "minecraft:air")  # definite effect


terrain = forAllBiomesExcept("biometweaker:block_rock_desert_trees")
terrain.set("genWeight", 2)

explicit = forBiomes("minecraft:ocean", "minecraft:river", "minecraft:desert", "minecraft:deep_ocean", "minecraft:beaches")
explicit.registerGenBlockRep("minecraft:water", "minecraft:air")  # definite effect
explicit.registerGenBiomeRep(brd)

superfluous = forBiomes(10, 11, 12, 13, 15, 17, 18, 19, 20, 23, 25, 26, 27, 28, 29, 30, 31, 33, 34, 36, 37, 41, 129, 130, 131, 132, 133, 134, 140, 149, 151, 155, 156, 158, 160, 161, 162, 163, 164, 165, 166, 167)
superfluous.remove()

# a tale of woe https://github.com/superckl/BiomeTweaker/wiki/On-the-Topic-of-Biome-Generation
hard_superfluous = forBiomes(1, 2, 3, 4, 5, 6, 14, 21, 22, 35, 38, 39, 32)
hard_superfluous.registerGenBiomeRep(brd)

all = forAllBiomes()

Tweaker.setStage("FINISHED_LOAD")
all.set("isSpawnBiome", false)
all.set("genWeight", 5)
brd.set("isSpawnBiome", true)
brd.set("genWeight", 1000)
brd_tree.set("genWeight", 500)
