# o g block rock desert desert
Tweaker.setWorld(1001)

# load our custom biomes
brd = forBiomes("biometweaker:block_rock_desert")
brd_tree = forBiomes("biometweaker:block_rock_desert_trees")
# do not seem to be able to set these in their respective script files?
brd.registerGenBlockRep("minecraft:water", "minecraft:air")
brd_tree.registerGenBlockRep("minecraft:water", "minecraft:air")

# we only want these weights in brd
brd.addToGeneration("WARM", 20000)
brd.addToGeneration("COOL", 20000)
brd.addToGeneration("ICY", 20000)
brd_tree.addToGeneration("WARM", 10000)
brd_tree.addToGeneration("COOL", 10000)
brd_tree.addToGeneration("ICY", 10000)

# update spooky biomes
spooky = forBiomes("abyssalcraft:darklands")
spooky.set("height", 0)
spooky.set("heightVariation", 0.1)
spooky.set("genVillages", false)
spooky.set("name", "Spooky Desert")
spooky.set("topBlock", "minecraft:sand")
spooky.set("fillerBlock", "minecraft:sandstone")
spooky.set("enableRain", false)
spooky.removeFeature("ANIMALS")
spooky.removeFeature("LAKE")           # unknown if effect?
spooky.removeDecoration("LAKE_WATER")  # unknown if effect?
spooky.removeDecoration("CACTUS")
spooky.removeDicTypes("FOREST")
spooky.removeDicTypes("HILLS")
spooky.registerGenBlockRep("minecraft:water", "minecraft:air")  # definite effect
spooky.removeAllSpawns("CREATURE")
spooky.removeAllSpawns("MONSTER")
spooky.removeAllSpawns("WATER_CREATURE")
spooky.removeAllSpawns("AMBIENT")
spooky.addSpawn("net.minecraft.entity.passive.EntityRabbit", "CREATURE", 2, 2, 3)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityAbyssalZombie", "MONSTER", 80, 1, 3)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityDepthsGhoul", "MONSTER", 60, 1, 5)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityAbyssalZombie", "MONSTER", 60, 1, 3)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowCreature", "MONSTER", 70, 3, 3)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowMonster", "MONSTER", 50, 2, 2)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowBeast", "MONSTER", 20, 1, 1)
spooky.addSpawn("net.minecraft.entity.monster.EntityHusk", "MONSTER", 80, 4, 4)
spooky.addSpawn("net.minecraft.entity.passive.EntityBat", "AMBIENT", 10, 8, 8)
spooky.addSpawn("com.shynieke.statues.entity.EntityStatueBat", "AMBIENT", 4, 1, 2)
spooky.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiBat", "AMBIENT", 10, 4, 8)


# update swampy biomes
swampy = forBiomes("abyssalcraft:coralium_infested_swamp")
swampy.set("height", 0.0)
swampy.set("heightVariation", 0.0)
swampy.set("genVillages", false)
swampy.set("name", "Lush Desert")
swampy.set("topBlock", "minecraft:sand")
swampy.set("fillerBlock", "minecraft:sandstone")
swampy.set("enableRain", false)
swampy.removeFeature("ANIMALS")
swampy.removeFeature("ICE")
swampy.removeFeature("LAVA")
swampy.removeFeature("LAKE")           # unknown if effect?
swampy.removeDecoration("LAKE_WATER")  # unknown if effect?
swampy.removeDecoration("CACTUS")
swampy.registerGenBlockRep("minecraft:water", "fossil:tar")
swampy.removeAllSpawns("CREATURE")
swampy.removeAllSpawns("MONSTER")
swampy.removeAllSpawns("WATER_CREATURE")
swampy.removeAllSpawns("AMBIENT")
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityAbyssalZombie", "MONSTER", 20, 1, 3)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityDepthsGhoul", "MONSTER", 30, 1, 5)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowCreature", "MONSTER", 70, 3, 3)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowMonster", "MONSTER", 50, 2, 2)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.EntityShadowBeast", "MONSTER", 20, 1, 1)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiAbyssalZombie", "MONSTER", 20, 1, 3)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiCreeper", "MONSTER", 20, 1, 2)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiGhoul", "MONSTER", 10, 1, 2)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiSpider", "MONSTER", 20, 1, 2)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiPlayer", "MONSTER", 80, 2, 5)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiSkeleton", "MONSTER", 20, 2, 5)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiZombie", "MONSTER", 20, 2, 5)
swampy.addSpawn("com.shynieke.statues.entity.EntityStatueBat", "AMBIENT", 4, 1, 2)
swampy.addSpawn("com.shinoow.abyssalcraft.common.entity.anti.EntityAntiBat", "AMBIENT", 10, 4, 8)

# a tale of woe https://github.com/superckl/BiomeTweaker/wiki/On-the-Topic-of-Biome-Generation
# note the list goes much further than that (distinction between superfluous and "hard" superfluous)
superfluous = forBiomes(10, 11, 12, 13, 19, 27, 28, 29, 30, 31, 33, 34, 44, 47, 48, 49, 50, 60, 131, 140, 155, 156, 157, 158, 160, 161, 162)
superfluous.remove()

hard_superfluous = forBiomes(0, 1, 2, 3, 4, 5, 6, 7, 14, 15, 16, 17, 18, 20, 21, 22, 23, 24, 25, 26, 32, 35, 36, 37, 38, 39, 129, 130, 131, 132, 133, 134, 149, 151, 163, 164, 165, 166, 167)
hard_superfluous.registerGenBiomeRep(brd)
hard_superfluous.set("genWeight", 1)
hard_superfluous.set("isSpawnBiome", false)
hard_superfluous.registerGenBlockRep("minecraft:water", "minecraft:air")

brd.set("isSpawnBiome", true)
brd.set("genWeight", 1000)
brd_tree.set("isSpawnBiome", false)
brd_tree.set("genWeight", 500)
swampy.set("isSpawnBiome", false)
swampy.set("genWeight", 20)
spooky.set("isSpawnBiome", false)
spooky.set("genWeight", 20)
